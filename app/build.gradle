apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.mingmin.newtaipeipublicparking"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        prod {
            dimension "default"
            testInstrumentationRunnerArguments clearPackageData: 'true'
        }
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android support libs
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0-alpha03'

    // Local Unit Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest:2.1'
    testImplementation 'io.mockk:mockk:1.9.1'

    // Android test support libs
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test:rules:1.2.0-alpha03'

    // Espresso UI Test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0-alpha03'

    // UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Orchestrator
    androidTestUtil 'androidx.test:orchestrator:1.2.0-alpha03'

    // Jackson
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7'

    // Google Map
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
}
